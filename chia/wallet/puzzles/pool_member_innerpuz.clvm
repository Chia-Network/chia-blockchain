(mod (POOL_PUZZLE_HASH
      P2_SINGLETON_PUZHASH
      OWNER_PUBKEY
      POOL_REWARD_PREFIX
      ESCAPE_MODE_PUZHASH
      (my_id my_full_puzzle_hash my_inner_puzzle_hash my_amount parent_info mod_hash launcher_id launcher_puzzle_hash)
      spend_type
      pool_reward_amount
      pool_reward_height
      key_value_list)


  ; POOL_PUZZLE_HASH is commitment to the pool's puzzle hash
  ; P2_SINGLETON_PUZHASH is the puzzlehash for your pay to singleton puzzle
  ; OWNER_PUBKEY is the farmer pubkey which authorises an escape attempt
  ; POOL_REWARD_PREFIX is network-specific data (mainnet vs testnet) that helps determine if a coin is a pool reward
  ; ESCAPE_MODE_PUZHASH is the puzzle_hash you'll go to when you iniate the leaving process

  ; spend_type is: 0 for absorbing money, 1 to escape

  ; absorb_coin_id and my_puzzle_hash are only used for absorbtion case

  (include condition_codes.clvm)

  ; takes a lisp tree and returns the hash of it
  (defun sha256tree1 (TREE)
      (if (l TREE)
          (sha256 2 (sha256tree1 (f TREE)) (sha256tree1 (r TREE)))
          (sha256 1 TREE)
      )
  )

  (defun-inline calculate_pool_reward (pool_reward_height P2_SINGLETON_PUZHASH POOL_REWARD_PREFIX pool_reward_amount)
    (sha256 (logior POOL_REWARD_PREFIX pool_reward_height) P2_SINGLETON_PUZHASH pool_reward_amount)
  )

  (defun-inline absorb_pool_reward (POOL_PUZZLE_HASH my_inner_puzzle_hash my_amount pool_reward_amount pool_reward_id)
    (list
        (list CREATE_COIN my_inner_puzzle_hash my_amount)
        (list CREATE_COIN POOL_PUZZLE_HASH pool_reward_amount)
        (list CREATE_PUZZLE_ANNOUNCEMENT pool_reward_id)
        (list ASSERT_COIN_ANNOUNCEMENT (sha256 pool_reward_id 0x80))
    )
  )

  ; main

  (if spend_type
    (list (list AGG_SIG_ME OWNER_PUBKEY (sha256tree1 key_value_list ()))
          (list CREATE_COIN ESCAPE_MODE_PUZHASH my_amount)

    )
    (absorb_pool_reward POOL_PUZZLE_HASH
                        my_inner_puzzle_hash
                        my_amount
                        pool_reward_amount
                        (calculate_pool_reward pool_reward_height P2_SINGLETON_PUZHASH POOL_REWARD_PREFIX pool_reward_amount))
  )
)
