; This apparent monstrosity of a chialisp file really does a very simple thing and gets it relative ugliness from
; special attention paid to optimizing it.
;
; The goal of the function is to make sure that some delegated puzzle (inner_most_puzzle_hash) is wrapped by a number
; of wrappers (WRAPPER_STACK) that use a specific base puzzle (ADD_WRAPPER_MOD). It uses the information in its solution
; to calculate the hash of the following and assert it matches the delegated puzzle being validated:
;
; (a (q . WRAPPER_MOD) (c (q . WRAPPER) (c delegated_puzzle 1)))
(mod
  (
    QUOTED_ADD_WRAPPER_MOD_HASH  ; The mod hash of the quoted wrapper add-er i.e. (q . ADD_WRAPPER_MOD)
    WRAPPER_STACK  ; The list of wrapper hashes (pre-quoted like above) to be run on the delegated puzzle
    delegated_puzzle_hash  ; The hash of the delegated puzzle to validate
    inner_most_puzzle_hash  ; hash of the thing being wrapped by all of these puzzles
  )

  (defconstant TWO 2)
  (defconstant SHA_1_NIL 0x4bf5122f344554c53bde2ebb8cd2b7e3d1600ad631c385a5d7cce23c7785459a)
  (defconstant SHA_1_1 0x9dcf97a184f32623d11a73124ceb99a5709b083721e878a16d78f596718ba7b2)
  (defconstant SHA_1_C 0xa8d5dd63fba471ebcb1f3e8f7c1e1879b7152a6e7298a91ce119a63400ade7c5)
  (defconstant SHA_1_A 0xa12871fee210fb8619291eaea194581cbd2531e4b23759d225f6806923f63222)
  (defconstant SHA_1_&_NIL 0xba4484b961b7a2369d948d06c55b64bdbfaffb326bc13b490ab1215dd33d8d46)

  (defun-inline branch_hash (left right)
    (sha256 TWO left right)
  )

  (defun wrap_inner_most_puzzle_hash (QUOTED_ADD_WRAPPER_MOD_HASH WRAPPER_STACK inner_most_puzzle_hash)
    (if WRAPPER_STACK
        (branch_hash
          SHA_1_A
          (branch_hash
            QUOTED_ADD_WRAPPER_MOD_HASH
            (branch_hash
              (branch_hash
                SHA_1_C
                (branch_hash
                  (f WRAPPER_STACK)
                  (branch_hash
                    (branch_hash
                      SHA_1_C
                      (branch_hash
                        (branch_hash
                          SHA_1_1
                          (wrap_inner_most_puzzle_hash QUOTED_ADD_WRAPPER_MOD_HASH (r WRAPPER_STACK) inner_most_puzzle_hash)
                        )
                        SHA_1_&_NIL
                      )
                    )
                    SHA_1_NIL
                  )
                )
              )
              SHA_1_NIL
            )
          )
        )
        inner_most_puzzle_hash
    )
  )

  (if (= delegated_puzzle_hash (wrap_inner_most_puzzle_hash QUOTED_ADD_WRAPPER_MOD_HASH WRAPPER_STACK inner_most_puzzle_hash))
      ()
      (x)
  )
)
