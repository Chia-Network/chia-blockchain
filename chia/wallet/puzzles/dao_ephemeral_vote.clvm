(mod (
  PROPOSAL_MOD_HASH
  SINGLETON_MOD_HASH
  SINGLETON_LAUNCHER_PUZHASH
  LOCKUP_MOD_HASH
  EPHEMERAL_VOTE_MODHASH
  CAT_MOD_HASH
  CAT_TAIL
  LOCKUP_TIME
  proposal_id
  previous_votes
  my_amount  ; this is the weight of your vote
  vote_info  ; this is the information about what to do with your vote  - atm just 1 for yes or 0 for no
  pubkey
  my_id
  proposal_curry_vals  ; list of (PROPOSAL_TIMER_MOD_HASH EPHEMERAL_VOTE_PUZHASH CURRENT_CAT_ISSUANCE PROPOSAL_PASS_PERCENTAGE TREASURY_ID PROPOSAL_TIMELOCK current_votes total_votes INNERPUZHASH) - I know this is horrible, but what can you do?
)
  (include condition_codes.clvm)
  (include curry-and-treehash.clinc)

  (defun calculate_singleton_puzzle_hash (PROPOSAL_SINGLETON_STRUCT inner_puzzle_hash)
     (puzzle-hash-of-curried-function (f PROPOSAL_SINGLETON_STRUCT)
                                      inner_puzzle_hash
                                      (sha256tree PROPOSAL_SINGLETON_STRUCT)
     )
  )

  (defun calculate_lockup_puzzlehash (PROPOSAL_MOD_HASH SINGLETON_MOD_HASH SINGLETON_LAUNCHER_PUZHASH LOCKUP_MOD_HASH EPHEMERAL_VOTE_MODHASH CAT_MOD_HASH CAT_TAIL LOCKUP_TIME previous_votes pubkey)
    (puzzle-hash-of-curried-function LOCKUP_MOD_HASH
                                     (sha256 ONE pubkey)
                                     (sha256 ONE LOCKUP_TIME)
                                     (sha256tree previous_votes)
                                     (sha256 ONE CAT_TAIL)
                                     (sha256 ONE CAT_MOD_HASH)
                                     (sha256 ONE EPHEMERAL_VOTE_MODHASH)
                                     (sha256 ONE LOCKUP_MOD_HASH)
    )
  )

  (defun calculate_proposal_puzzlehash (
    PROPOSAL_MOD_HASH
    CAT_MOD_HASH
    EPHEMERAL_VOTE_MODHASH
    CAT_TAIL
    proposal_singleton_struct
    (
      TREASURY_MOD_HASH
      PROPOSAL_TIMER_MOD_HASH
      EPHEMERAL_VOTE_PUZHASH
      CURRENT_CAT_ISSUANCE
      PROPOSAL_PASS_PERCENTAGE
      TREASURY_ID
      PROPOSAL_TIMELOCK
      SUM_VOTES
      TOTAL_VOTES
      INNERPUZHASH
    )
  )
    (calculate_singleton_puzzle_hash
      proposal_singleton_struct
      (puzzle-hash-of-curried-function PROPOSAL_MOD_HASH
        INNERPUZHASH
        (sha256 ONE TOTAL_VOTES)
        (sha256 ONE SUM_VOTES)
        (sha256 ONE PROPOSAL_TIMELOCK)
        (sha256 ONE TREASURY_ID)
        (sha256 ONE PROPOSAL_PASS_PERCENTAGE)
        (sha256 ONE CURRENT_CAT_ISSUANCE)
        (sha256 ONE CAT_TAIL)
        (sha256 ONE EPHEMERAL_VOTE_PUZHASH)  ; this is the mod already curried with what it needs - should be a constant
        (sha256 ONE TREASURY_MOD_HASH)
        (sha256 ONE CAT_MOD_HASH)
        (sha256 ONE PROPOSAL_TIMER_MOD_HASH)
        (sha256 ONE PROPOSAL_MOD_HASH)
        (sha256tree proposal_singleton_struct)
      )
    )
  )

  ; main
  (list

    (list ASSERT_PUZZLE_ANNOUNCEMENT (sha256 (calculate_singleton_puzzle_hash (c SINGLETON_MOD_HASH (c proposal_id SINGLETON_LAUNCHER_PUZHASH)) (calculate_proposal_puzzlehash PROPOSAL_MOD_HASH CAT_MOD_HASH EPHEMERAL_VOTE_MODHASH CAT_TAIL (c SINGLETON_MOD_HASH (c proposal_id SINGLETON_LAUNCHER_PUZHASH)) proposal_curry_vals)) my_id))

    (list CREATE_COIN_ANNOUNCEMENT (sha256tree (list proposal_id previous_votes vote_info pubkey)))  ; this is for the lockup to assert to prevent it from double voting (it MUST assert this due to its mod rules)
                                                                                                             ; the above also gives this coin's solution some security so MAKE SURE to assert this the first time too
    (list CREATE_PUZZLE_ANNOUNCEMENT (sha256tree (list proposal_id my_amount vote_info my_id)))  ; this is for the proposal to assert to count your votes

    (list CREATE_COIN (calculate_lockup_puzzlehash PROPOSAL_MOD_HASH SINGLETON_MOD_HASH SINGLETON_LAUNCHER_PUZHASH LOCKUP_MOD_HASH EPHEMERAL_VOTE_MODHASH CAT_MOD_HASH CAT_TAIL LOCKUP_TIME (c proposal_id previous_votes) pubkey) my_amount)  ; move to lockup coin - this line also adds the new vote to your previous votes

    (list ASSERT_MY_PARENT_ID (calculate_lockup_puzzlehash PROPOSAL_MOD_HASH SINGLETON_MOD_HASH SINGLETON_LAUNCHER_PUZHASH LOCKUP_MOD_HASH EPHEMERAL_VOTE_MODHASH CAT_MOD_HASH CAT_TAIL LOCKUP_TIME previous_votes pubkey))  ; we must come from the voting state to prevent flash loan from God attack

    (list ASSERT_MY_AMOUNT my_amount)  ; make sure you have as much voting power as you say you do

    (list ASSERT_MY_COIN_ID my_id)
  )
)
